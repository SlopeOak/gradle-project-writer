task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

tasks.build.dependsOn tasks.sourcesJar

publishing {
    publications {
        gradleProjectWriter(MavenPublication) {
            version project.version
            artifactId = "${project.group}.project-writer.gradle.plugin"
            groupId = "${project.group}.project-writer"

            pom {
                name = 'Project writer plugin'
                description = 'Write a project to a file.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'SlopeOak'
                        name = 'Stuart Sinclair'
                    }
                }

                withXml {
                    def root = asNode()
                    def dependency = root.appendNode('dependencies').appendNode('dependency')
                    dependency.appendNode('groupId', project.group)
                    dependency.appendNode('artifactId', 'project-writer')
                    dependency.appendNode('version', project.version)
                    dependency.appendNode('classifier', 'all')
                }
            }
        }

        pluginShadow(MavenPublication) {
            groupId 'com.github.slopeoak'
            artifactId 'project-writer'
            artifact shadowJar
            artifact sourcesJar
        }
    }
}

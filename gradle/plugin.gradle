gradlePlugin {
    plugins {
        projectWriter {
            id = 'com.github.slopeoak.project-writer'
            implementationClass = 'com.github.slopeoak.GradleProjectWriterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/SlopeOak/gradle-project-writer'
    vcsUrl = 'https://github.com/SlopeOak/gradle-project-writer'
    description = 'Write the state of a project instance to a build file.'
    tags = ['projectwriter']

    plugins {
        projectWriter {
            displayName = 'Project writer plugin'
        }
    }
}

jar.enabled = false
shadowJar.classifier = ''

publishing {
    publications {
        pluginMaven(MavenPublication) {
            shadow.component(it)

            pom {
                name = 'Project writer plugin'
                description = 'Write a project to a file.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'SlopeOak'
                        name = 'Stuart Sinclair'
                    }
                }
            }

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.artifactId.text() == 'library'
                }.each() {
                    it.parent().remove(it)
                }

                asNode().'dependencies'.findAll() { Node deps ->
                    deps.children().size() == 0
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
